
#Область ПрограмныйИнтерфейс

Функция Настройки() Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БИТС_Настройки.Наименование КАК Наименование,
		|	БИТС_Настройки.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.БИТС_Настройки КАК БИТС_Настройки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат[Выборка.Наименование] = Выборка.Значение;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция Спринт(URL, Группа) Экспорт
	
	Результат = Новый Соответствие;
	
	Фильтр = Новый Структура("GROUP_ID, STATUS", Группа, "active");
	Ответ = OPI_Bitrix24.ПолучитьСписокСпринтов(URL, Фильтр);
	Если Ответ["result"] = Неопределено Тогда 
		Возврат Результат;
	КонецЕсли;
	
	Спринт = Ответ["result"][0];
	
	Результат["SPRINT_ID"] = Спринт["id"];
	Результат["SPRINT_NAME"] = Спринт["name"];
	Результат["SPRINT_START"] = Спринт["dateStart"];
	Результат["SPRINT_END"] = Спринт["dateEnd"];
	
	Возврат Результат;
	
КонецФункции

Функция Стадии(URL, Спринт) Экспорт
	
	Результат = Новый Соответствие;
	
	Ответ = OPI_Bitrix24.ПолучитьСтадииКанбанаСкрама(URL, Спринт);
	Если Ответ["result"] = Неопределено Тогда 
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Стадия Из Ответ["result"] Цикл
		Результат[Стадия["id"]] = Новый Структура("Наименование, Заголовок");
		Результат[Стадия["id"]].Наименование = БИТС_Служебный.CamelCase(Стадия["name"]);
		Результат[Стадия["id"]].Заголовок = СокрЛП(Стадия["name"]);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти