
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиФормы();
	СтадииСпринта = БИТС_СкрамСервер.Стадии(URL_API, SPRINT_ID);
	ЗадачиСпринта = БИТС_ЗадачиСервер.Получить(URL_API, GROUP_ID, SPRINT_ID);
	СозданиеЭлементовФормы(СтадииСпринта);
	ЗаполнитьТаблицы(СтадииСпринта, ЗадачиСпринта);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

#Область КомандыОбщие

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВремяЗаСпринт(Команда)
	
	СтадииСпринта = БИТС_СкрамСервер.Стадии(URL_API, SPRINT_ID);
	ВремяЗаСпринт = ВремяЗаСпринтНаСервере();
	Для Каждого Стадия Из СтадииСпринта Цикл
		ИмяЭлемента = "Задачи" + Стадия.Значение["Наименование"];
		Для Каждого Строка Из ЭтаФорма[ИмяЭлемента] Цикл
			
			ЗатратыЗадачи = ВремяЗаСпринт[Строка["id"]];
			Если ЗатратыЗадачи = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Строка["timeSpentInLogs"] = Строка(ЗатратыЗадачи["Всего"]);
			Строка["ВремяЗаСпринт"] = Строка(ЗатратыЗадачи["ЗаСпринт"]); 
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьРезультаты(Команда)
	
	СтадииСпринта = БИТС_СкрамСервер.Стадии(URL_API, SPRINT_ID);
	РезультатыЗадач = ПолучитьРезультатыНаСервере();
	Для Каждого Стадия Из СтадииСпринта Цикл
		ИмяЭлемента = "Задачи" + Стадия.Значение["Наименование"];
		Для Каждого Строка Из ЭтаФорма[ИмяЭлемента] Цикл
			
			РезультатыЗадачи = РезультатыЗадач[Строка["id"]];
			Если РезультатыЗадачи = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// TODO: Добавить результаты
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Дайджест(Команда)
	Дайджест = ДайджестНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	ОткрытьФорму("РегистрСведений.БИТС_Настройки.ФормаСписка",,,,,,, РежимОткрытияОкнаФормы.Независимый);
КонецПроцедуры

#КонецОбласти

#Область КомандыТаблиц

&НаКлиенте
Процедура ВидимостьСтадии(Команда) 
	
	ОсновныеЦвета = БИТС_ФормаСервер.ОсновныеЦвета();
	Если ЭтаФорма.ТекущийЭлемент.ЦветФона = ОсновныеЦвета["Оранжевый"] Тогда
		ЭтаФорма.ТекущийЭлемент.ЦветФона = ОсновныеЦвета["Зеленый"];
	Иначе
		ЭтаФорма.ТекущийЭлемент.ЦветФона = ОсновныеЦвета["Оранжевый"];
	КонецЕсли;
	
	ИмяГруппы = СтрЗаменить(ЭтаФорма.ТекущийЭлемент.Имя, "ВидимостьСтадии", "ГруппаСтадия");
	Элементы[ИмяГруппы].Видимость = НЕ Элементы[ИмяГруппы].Видимость; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	ИмяТаблицы = СтрЗаменить(ЭтаФорма.ТекущийЭлемент.Имя, "ВыделитьВсе", "");
	Для Каждого Строка Из ЭтаФорма[ИмяТаблицы] Цикл
		Строка.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	ИмяТаблицы = СтрЗаменить(ЭтаФорма.ТекущийЭлемент.Имя, "СнятьВсе", "");
	Для Каждого Строка Из ЭтаФорма[ИмяТаблицы] Цикл
		Строка.Выбран = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастройкиФормы()
	
	Скрам = БИТС_СкрамСервер.Настройки();
	Спринт = БИТС_СкрамСервер.Спринты(Скрам["URL_API"], Скрам["GROUP_ID"], Истина);
	
	Для Каждого Настройка Из Скрам Цикл
		ЭтаФорма[Настройка.Ключ] = Настройка.Значение;
	КонецЦикла;
	
	Для Каждого Настройка Из Спринт Цикл
		ЭтаФорма[Настройка.Ключ] = Настройка.Значение;
	КонецЦикла;
	 
	ЭтаФорма.Заголовок = БИТС_СлужебныйСервер.ПредставлениеСпринта(Спринт);
	
КонецПроцедуры

Процедура СозданиеЭлементовФормы(Знач СтадииСпринта)
	
	БИТС_ФормаСервер.НоваяОбычнаяГруппа(ЭтотОбъект, "ВыборСтадии", 1);
	ГруппаКнопок = Элементы["ВыборСтадии"];
	БИТС_ФормаСервер.НоваяКоманда(ЭтотОбъект, "ВидимостьСтадии", "ВидимостьСтадии", "Видимость стадии");
	
	Для Каждого Ключ Из БИТС_СлужебныйСервер.ОтсортированныеКлючи(СтадииСпринта) Цикл
		НаименованиеСтадии = СтадииСпринта[Ключ]["Наименование"];
		ИмяКнопки = "ВидимостьСтадии" + НаименованиеСтадии; 
		БИТС_ФормаСервер.НоваяКнопкаФормы(ЭтотОбъект, ИмяКнопки, ГруппаКнопок, "ВидимостьСтадии");
		Кнопка = Элементы[ИмяКнопки];
		Кнопка.Заголовок = СтадииСпринта[Ключ]["Заголовок"];
	КонецЦикла;
	
КонецПроцедуры

#Область РаботаСТаблицами

&НаСервере
Процедура ОбновитьНаСервере()
	
	НастройкиФормы();
	СтадииСпринта = БИТС_СкрамСервер.Стадии(URL_API, SPRINT_ID, Истина);
	ЗадачиСпринта = БИТС_ЗадачиСервер.Получить(URL_API, GROUP_ID, SPRINT_ID, Истина);
	СозданиеЭлементовФормы(СтадииСпринта);
	ЗаполнитьТаблицы(СтадииСпринта, ЗадачиСпринта);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицы(СтадииСпринта, ЗадачиСпринта)
	
	ОчиститьТаблицы();
	
	Для Каждого Задача Из ЗадачиСпринта Цикл
		ИмяЭлемента = "Задачи" + СтадииСпринта[Задача["stageId"]]["Наименование"];
		СтрокаСтадии = ЭтаФорма[ИмяЭлемента].Добавить();
		Для Каждого Поле Из Задача Цикл
			
			СтрокаСтадии[Поле.Ключ] = Расшифровка(Поле);
			СтрокаСтадии["НадписьТаблицыВремя"] = "Время в часах";
			СтрокаСтадии["НадписьТаблицыВсего"] = "Всего:";
			СтрокаСтадии["НадписьТаблицыЗаСпринт"] = "Спринт:";
			СтрокаСтадии["НадписьТаблицыОценка"] = "Оценка:";
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицы()
	
	СтадииСпринта = БИТС_СкрамСервер.Стадии(URL_API, SPRINT_ID);
	ЭтаФорма.TASKS.Очистить();
	Для Каждого Стадия Из СтадииСпринта Цикл
		ИмяЭлемента = "Задачи" + Стадия.Значение["Наименование"]; 
		ЭтаФорма[ИмяЭлемента].Очистить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСЗадачами

&НаСервере
Функция ДайджестНаСервере()
	
	Результат = "";
	
	ВыбранныеЗадачи = ВыбранныеЗадачи(); 
	Если ВыбранныеЗадачи.Количество() = 0 Тогда
		Сообщить("Задачи не выбраны");
		Возврат Результат;
	КонецЕсли;
	
	// РезультатыЗадач = ЧетоТам(ВыбранныеЗадачи);
	// СоответствияРезультатов = БИТС_НовостнойДайджест.СоответствияРезультатов(РезультатыЗадач);
	СоответствияРезультатов = Новый Соответствие;
	Результат = БИТС_ДайджестСервер.СформироватьДокумент(ТекущаяДатаСеанса(), СоответствияРезультатов);
	// TODO: Сформировать MD дайджест
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьРезультатыНаСервере()
	
	Результат = Новый Соответствие;
	
	ВыбранныеЗадачи = ВыбранныеЗадачи(); 
	Если ВыбранныеЗадачи.Количество() = 0 Тогда
		Сообщить("Задачи не выбраны");
		Возврат Результат;
	КонецЕсли; 
	
	Результат = БИТС_РезультатыСервер.ПолучитьДляЗадач(URL_API, ВыбранныеЗадачи);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ВремяЗаСпринтНаСервере()
	
	Результат = Новый Соответствие;
	
	ВыбранныеЗадачи = ВыбранныеЗадачи(); 
	Если ВыбранныеЗадачи.Количество() = 0 Тогда
		Сообщить("Задачи не выбраны");
		Возврат Результат;
	КонецЕсли;
	
	Результат = БИТС_ЗадачиСервер.ВремяЗаСпринт(URL_API, ВыбранныеЗадачи, SPRINT_START, SPRINT_END);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ВыбранныеЗадачи()
	
	Результат = Новый Массив;
	
	СтадииСпринта = БИТС_СкрамСервер.Стадии(URL_API, SPRINT_ID);
	Для Каждого Стадия Из СтадииСпринта Цикл
		ИмяЭлемента = "Задачи" + Стадия.Значение["Наименование"];
		Для Каждого Задача Из ЭтаФорма[ИмяЭлемента] Цикл
			Если Задача.Выбран Тогда
				Результат.Добавить(Задача.id);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция Расшифровка(Поле)
	
	Результат = "";
	
	Если Поле.Ключ = "timeSpentInLogs" ИЛИ Поле.Ключ = "timeEstimate" Тогда
		Если ЗначениеЗаполнено(Поле.Значение) Тогда
			Результат = Строка(БИТС_СлужебныйСервер.СекундыВЧасы(Поле.Значение));
		Иначе
			Результат = "0";
		КонецЕсли;
	ИначеЕсли Поле.Ключ = "description" Тогда
		РегулярноеВыражение = "\[(/?[^\]]+)\]";
		Результат = СтрЗаменитьПоРегулярномуВыражению(Поле.Значение, РегулярноеВыражение, "");
	Иначе
		Результат = Поле.Значение;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти  
